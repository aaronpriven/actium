#Actium/LineInfo.pm

# Subversion: $Id$

package Actium::LineInfo 0.006;

use 5.016;
use warnings;

use Actium::Preamble;
use Actium::Sorting::Line('sortbyline');
use Actium::EffectiveDate('effectivedate');

use Params::Validate;
use HTML::Entities;

use Sub::Exporter -setup =>
  { exports => [qw(line_descriptions line_classification line_descrip_html )] };

sub line_descriptions {

    my $actiumdb = shift;

    my $descrip_of_r = $actiumdb->all_in_column_key(
        {   TABLE  => 'Lines',
            COLUMN => 'Description',
            WHERE  => 'Active = ? AND '
              . 'Line <> ? AND Line <> ? AND Line <> ? AND Line <> ?',
            BIND_VALUES => [ 'Yes', 'BSD', 'BSN', 'DB', 'DB1' ],
        }
    );

    return $descrip_of_r;
}

sub line_descrips_by_class {

    my $descrip_of_r = line_descriptions(@_);

    my %linedescrip_of_class;

    foreach my $line ( sortbyline keys %{$descrip_of_r} ) {
        my $class = line_classification($line);
        push @{ $linedescrip_of_class{$class} },
          [ $line, $descrip_of_r->{$line} ];
    }

    my $total = 0;

    foreach my $class ( keys %linedescrip_of_class ) {

        my $count = scalar @{ $linedescrip_of_class{$class} };
        say "$class: $count";
        $total += $count;

    }

    say "Total: $total";

    return \%linedescrip_of_class;
} ## tidy end: sub line_descrips_by_class

sub line_descrip_html {

    my %params = validate(
        @_,
        {   database => 1,
            signup   => 1,
            version  => 1,
        }
    );

    my ( $actiumdb, $signup, $current_version )
      = @params{qw[database signup version]};

    my $effdate = effectivedate($signup);

    my $linedescrip_of_class_r = line_descrips_by_class($actiumdb);

    my $html
      = "\n<!--\n    Do not edit this file!"
      . "It is automatically generated from a program.\n-->"
      . _ldh_header($effdate);
      
    my $total = 0;

    foreach my $class ( qw/Local Transbay/, 'All Nighter', 'Supplementary' ) {
        # heading
        my $count = scalar @{ $linedescrip_of_class_r->{$class} };
        my $pub = "$class Lines";
        my $anchor = $class eq 'All Nighter' ? 'AllNighter' : $class;

        $html
          .= qq{<table style="border-collapse: collapse;" border="1">}
          . qq{<caption style="padding-top: 1.2em;"><strong><a name="$anchor">}
          . qq{$pub</a></strong></caption>};

        $html
          .= '<thead><tr><th style="background-color: silver;">Lines</th>'
          . '<th style="background-color: silver;">Description</th>';

        $html
          .= "\n" . '<th style="background-color: silver;">Links</th></thead>';

        $html .= '<tbody>';

        my @lines = @{ $linedescrip_of_class_r->{$class} };

        foreach my $ld_r (@lines) {
            my ( $line, $desc ) = @{$ld_r};
            $desc = HTML::Entities::encode_entities($desc);

            $html .= qq{<tr><td style="text-align: center;">$line</td>};
            $html .= qq{<td style="padding: 2pt;">$desc</td>};
            $html .= '<td style="text-align: center;">';
            $html
              .= qq{<a href="http://www.actransit.org/maps/maps_results.php?ms_view_type=2&maps_line=$line&version_id=$current_version&map_submit=Get+Map">Map</a>};
            $html .= "<br>";
            $html
              .= qq{<a href="http://www.actransit.org/maps/schedule_results.php?quick_line=$line&Go=Go">Schedule</a>};
            $html .= '</td></tr>' . "\n";

        }

        $html .= '</tbody></table>' . "\n";
        $html .= "<p>($class lines: $count)</p>\n";
        
        $total += $count;

    } ## tidy end: foreach my $class ( qw/Local Transbay/...)

    $html .= "<p>(Total lines: $total)</p>\n";

    $html .= _ldh_footer();

    return $html;

} ## tidy end: sub line_descrip_html

sub _ldh_footer {

    my $footer = <<'EOF';
  <p> <strong>Find <a href="http://www.actransit.org/maps/index.php">maps 
    &amp; schedules</a> for these lines.</strong> Or call 511 and say 
    &quot;AC Transit&quot; for trip-planning assistance.</p>
EOF

    $footer =~ s/\n/ /g;
    $footer =~ s/ +/ /g;

    return $footer;

}

sub _ldh_header {

    my $effdate = shift;

    my $header = <<"EOF";
<p>Effective $effdate</p>

<h2>About Bus Line Numbers and Letters</h2>

<p>AC Transit's numbered lines serve the East Bay. </p>

<p>Lines 1&ndash;299 operate normal hours. Normal hours are, at a minimum,
the commute periods, 6 a.m. – 9 a.m. and 4 p.m. – 6 p.m., weekdays.
Almost all operate all day on weekdays and many operate weekday
evenings and weekends as well. Lines 200-299 serve the areas of
Fremont and Newark, while other lines serve other parts of the East
Bay from Richmond to Hayward.</p>

<p>Lines 300&ndash;399 do not operate during the commute period. They
operate at other times of the day: for example, mid-days only,
weekends only, or evenings only. Some lines operate only a few days
per week (for example, Tuesdays and Thursdays).</p>

<p>Lines 600&ndash;699 are timed to match the instruction hours of local
schools, and operate only when schools are in session. They may
have altered schedules when local schools have minimum day or
alternative schedules. These lines are open to all passengers at
regular fares.</p>

<p>Lines 800&ndash;899 are All Nighter lines, operating from 1 a.m. – 5
a.m. daily. Some may operate somewhat earlier or later (especially
on weekends).</p>

<p>Lettered lines (A-Z) are Transbay routes, connecting the East
Bay to San Francisco or the Peninsula.</p>

<hr/>

<p><strong>Go to the <a href="http://www.actransit.org/maps/index.php">Maps &amp; Schedules</a> page.
</strong></p>

  <p > 
  <a href="#Local">Local Lines</a><br>
  <a href="#Transbay">Transbay Lines</a><br>
  <a href="#AllNighter">All Nighter Lines</a> <br>
  <a href="#Supplementary">Supplementary Lines</a><br>
</p>

<hr>
EOF

    $header =~ s/\n/ /g;
    $header =~ s/ +/ /g;

    return $header;

} ## tidy end: sub _ldh_header

sub line_classification {

    my $line = shift;

    return 'Transbay' if $line eq '800' or $line =~ /\A[A-Z]/;
    return 'Supplementary' if $line =~ /\A6\d\d/;
    return 'All Nighter'   if $line =~ /\A8\d\d/;
    return 'Local';

}

1;

__END__
